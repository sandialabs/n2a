options
{
    STATIC = false;
    UNICODE_INPUT = true;
    MULTI = true;
    NODE_DEFAULT_VOID = true;          // Only create Node classes for the #Tokens
}

PARSER_BEGIN(ExpressionParser)

/*
Copyright 2013-2022 National Technology & Engineering Solutions of Sandia, LLC (NTESS).
Under the terms of Contract DE-NA0003525 with NTESS,
the U.S. Government retains certain rights in this software.
*/

package gov.sandia.n2a.language.parse;

import gov.sandia.n2a.language.Identifier;
import gov.sandia.n2a.language.UnitValue;
import gov.sandia.n2a.language.type.Text;

import java.io.StringReader;
import java.util.Map;

import javax.measure.Unit;
import javax.measure.format.MeasurementParseException;
import systems.uom.ucum.internal.format.TokenException;

public class ExpressionParser
{
    public static Map<String,Unit<?>> namedUnits;  // Alternate unit names which should not go through the UCUM parser. null if none are active.

    public static SimpleNode parse (String line) throws gov.sandia.n2a.language.ParseException
    {
        try
        {
            ExpressionParser parser = new ExpressionParser (new StringReader (line));
            Node startNode = parser.Start ();
            SimpleNode result = (SimpleNode) startNode.jjtGetChild (0);
            return result;
        }
        catch (TokenMgrError t)
        {
            String column = t.getMessage ().split ("column ", 2)[1].trim ().split ("\\.", 2)[0];  // See TokenMgrError.LexicalError() for the structure of the string we are pulling apart here.
            throw new gov.sandia.n2a.language.ParseException ("Lexical error:", line, Integer.parseInt (column) - 1);
        }
        catch (ParseException t)
        {
            String message = t.getMessage ();
            if (message == null  ||  message.isEmpty ()) message = "Syntax error";
            message += ":";
            int column = 0;
            if (t.currentToken != null  &&  t.currentToken.next != null) column = t.currentToken.next.beginColumn;
            throw new gov.sandia.n2a.language.ParseException (message, line, column - 1);
        }
    }
}
PARSER_END(ExpressionParser)


// Tokens --------------------------------------------------------------------

TOKEN_MGR_DECLS:
{
    int parenDepth;  // For counting parentheses nesting in PossibleUnit mode
    int squareDepth; // Ditto for square bracket.
}

<*> SKIP:
{
    " " | "\t" | "\n"
}

<DEFAULT> TOKEN:
{
      <COMMA:   "," >
    | <SEMI:    ";" >
    | <GT:      ">" >
    | <LT:      "<" >
    | <ASSIGN:  "=" >
    | <EQ:      "==">
    | <LE:      "<=">
    | <GE:      ">=">
    | <NE:      "!=">
    | <AND:     "&&">
    | <OR:      "||">
    | <PLUS:    "+" >
    | <MINUS:   "-" >
    | <MUL:     "*" >
    | <HADAMUL: "&" >
    | <DIV:     "/" >
    | <MOD:     "%" >
    | <NOT:     "!" >
    | <POWER:   "^" >
    | <TILDE:   "~" >
    | <LSQ:     "[" >
    | <RSQ:     "]" >
    | <LRND:    "(" >
    | <RRND:    ")" >

    | <IDENTIFIER: <LETTER>(<LETTER>|<DIGIT>|"\u221e"|"."|" ")*("'")*>  // Notice that spaces are allowed inside an identifier
    | <#LETTER: ["$", "A"-"Z", "_", "a"-"z", "\u0080"-"\u221d", "\u221f"-"\uffff"]>  // Past lower-ASCII we don't care about character class, but skip infinity (\u221e). Infinity is the only digit-like character beyond lower ASCII that we care about.
    | <#DIGIT:  ["0"-"9"]>

    | <NUMBER:
          (<DIGIT>)+ "." (<DIGIT>)* (<EXPONENT>)?
        |            "." (<DIGIT>)+ (<EXPONENT>)?
        | (<DIGIT>)+                (<EXPONENT>)?
        | "0" ("x"|"X") (<DIGIT>|"a"|"A"|"b"|"B"|"c"|"C"|"d"|"D"|"e"|"E"|"f"|"F")+
        | "." ("i"|"I") ("n"|"N") ("f"|"F")
        | "." ("n"|"N") ("a"|"A") ("n"|"N")
        | "\u221e"  // infinity
        > : PossibleUnit
    | <#EXPONENT: ["e","E"] (["+","-"])? (<DIGIT>)+>

    | <STRING: "\"" ((~["\"","\\"]) | ("\\" ["n","t","b","r","f","\\","'","\""]))* "\"">
}

<PossibleUnit> TOKEN:
{
      <UNIT: (["\"","#","'","0"-":","?"-"Z","\\","_"-"{","}"])+>  // Everything in ASCII 33-126, except forbidden start characters.
    | <UCOMMA:   ","  > {matchedToken.kind = ExpressionParserConstants.COMMA;  }: DEFAULT
    | <USEMI:    ";"  > {matchedToken.kind = ExpressionParserConstants.SEMI;   }: DEFAULT
    | <UGT:      ">"  > {matchedToken.kind = ExpressionParserConstants.GT;     }: DEFAULT
    | <ULT:      "<"  > {matchedToken.kind = ExpressionParserConstants.LT;     }: DEFAULT
    | <UASSIGN:  "="  > {matchedToken.kind = ExpressionParserConstants.ASSIGN; }: DEFAULT
    | <UEQ:      "==" > {matchedToken.kind = ExpressionParserConstants.EQ;     }: DEFAULT
    | <ULE:      "<=" > {matchedToken.kind = ExpressionParserConstants.LE;     }: DEFAULT
    | <UGE:      ">=" > {matchedToken.kind = ExpressionParserConstants.GE;     }: DEFAULT
    | <UNE:      "!=" > {matchedToken.kind = ExpressionParserConstants.NE;     }: DEFAULT
    | <UAND:     "&&" > {matchedToken.kind = ExpressionParserConstants.AND;    }: DEFAULT
    | <UOR:      "||" > {matchedToken.kind = ExpressionParserConstants.OR;     }: DEFAULT
    | <UPLUS:    "+"  > {matchedToken.kind = ExpressionParserConstants.PLUS;   }: DEFAULT
    | <UMINUS:   "-"  > {matchedToken.kind = ExpressionParserConstants.MINUS;  }: DEFAULT
    | <UMUL:     "*"  > {matchedToken.kind = ExpressionParserConstants.MUL;    }: DEFAULT
    | <UHADAMUL: "&"  > {matchedToken.kind = ExpressionParserConstants.HADAMUL;}: DEFAULT
    | <UDIV:     "/"  > {matchedToken.kind = ExpressionParserConstants.DIV;    }: DEFAULT
    | <UMOD:     "%"  > {matchedToken.kind = ExpressionParserConstants.MOD;    }: DEFAULT
    | <UNOT:     "!"  > {matchedToken.kind = ExpressionParserConstants.NOT;    }: DEFAULT
    | <UPOWER:   "^"  > {matchedToken.kind = ExpressionParserConstants.POWER;  }: DEFAULT
    | <UTILDE:   "~"  > {matchedToken.kind = ExpressionParserConstants.TILDE;  }: DEFAULT
    | <PULSQ:    "["  > {matchedToken.kind = ExpressionParserConstants.UNIT; squareDepth = 1;}: FullUCUM
    | <PURSQ:    "]"  > {matchedToken.kind = ExpressionParserConstants.RSQ;    }: DEFAULT
    | <PULRND:   "("  > {matchedToken.kind = ExpressionParserConstants.UNIT; parenDepth  = 1;}: FullUCUM
    | <PURRND:   ")"  > {matchedToken.kind = ExpressionParserConstants.RRND;   }: DEFAULT
}

<FullUCUM> TOKEN:
{
      <UUNIT: (["!"-"'","*"-"Z","\\","^"-"~"])+>  // Everything in ASCII 33-126, except parentheses and square brackets
                   {matchedToken.kind = ExpressionParserConstants.UNIT;}
    | <ULRND: "("> {matchedToken.kind = ExpressionParserConstants.UNIT; parenDepth++;}
    | <URRND: ")"> {matchedToken.kind = ExpressionParserConstants.UNIT; parenDepth--;  if (parenDepth <= 0  &&  squareDepth <= 0) SwitchTo (PossibleUnit);}
    | <ULSQ:  "["> {matchedToken.kind = ExpressionParserConstants.UNIT; squareDepth++;}
    | <URSQ:  "]"> {matchedToken.kind = ExpressionParserConstants.UNIT; squareDepth--; if (parenDepth <= 0  &&  squareDepth <= 0) SwitchTo (PossibleUnit);}
}


// Grammar -------------------------------------------------------------------

ASTStart Start() #Start : {}
{
    CommaExpression() <EOF>
    {
        return jjtThis;
    }
}

void CommaExpression() #List : {}
{
    KeywordExpression() (<COMMA> KeywordExpression())*
    {
    	jjtThis.jjtSetValue("LIST");
    }
}

void KeywordExpression() : {Token t;}
{
    LOOKAHEAD(2)
    (t = <IDENTIFIER> <ASSIGN> OrExpression()
        {
            jjtThis.jjtSetValue (t.image);
        }
    ) #Keyword(1)
      |
    OrExpression()
}

void OrExpression() : {}
{
    AndExpression()
    (
        (<OR> AndExpression()
            {
                jjtThis.jjtSetValue("||");
            }
        ) #Operator(2)
    )*
}

void AndExpression() : {}
{
    EqualExpression()
    (
        (<AND> EqualExpression()
            {
                jjtThis.jjtSetValue("&&");
            }
        ) #Operator(2)
    )*
}

void EqualExpression() : {}
{
    RelationalExpression()
    (
        (<NE> RelationalExpression()
            {
                jjtThis.jjtSetValue("!=");
            }
        ) #Operator(2)
          |
        (<EQ> RelationalExpression()
            {
                jjtThis.jjtSetValue("==");
            }
        ) #Operator(2)
    )*
}

void RelationalExpression() : {}
{
    AdditiveExpression()
    (
        (<LT> AdditiveExpression()
            {
                jjtThis.jjtSetValue("<");
            }
        ) #Operator(2)
          |
        (<GT> AdditiveExpression()
            {
                jjtThis.jjtSetValue(">");
            }
        ) #Operator(2)
          |
        (<LE> AdditiveExpression()
            {
                jjtThis.jjtSetValue("<=");
            }
        ) #Operator(2)
          |
        (<GE> AdditiveExpression()
            {
                jjtThis.jjtSetValue(">=");
            }
        ) #Operator(2)
    )*
}

void AdditiveExpression() : {}
{
    MultiplicativeExpression()
    (
        (<PLUS> MultiplicativeExpression()
            {
                jjtThis.jjtSetValue("+");
            }
        ) #Operator(2)
          |
        (<MINUS> MultiplicativeExpression()
            {
                jjtThis.jjtSetValue("-");
            }
        ) #Operator(2)
    )*
}

void MultiplicativeExpression() : {}
{
    PowerExpression()
    (
        (<MUL> PowerExpression()
            {
                jjtThis.jjtSetValue("*");
            }
        ) #Operator(2)
          |
        (<HADAMUL> PowerExpression()
            {
                jjtThis.jjtSetValue("&");
            }
        ) #Operator(2)
          |
        (<DIV> PowerExpression()
            {
                jjtThis.jjtSetValue("/");
            }
        ) #Operator(2)
          |
        (<MOD> PowerExpression()
            {
                jjtThis.jjtSetValue("%");
            }
        ) #Operator(2)
    )*
}

// See https://www.engr.mun.ca/~theo/Misc/exp_parsing.htm, section "classic solution".
void PowerExpression() : {}
{
    PrimaryExpression()
    (
        LOOKAHEAD(1)
        (<POWER> PowerExpression()
            {
                jjtThis.jjtSetValue("^");
            }
        ) #Operator(2)
    )?
}

void UnaryExpression() : {}
{
    (<MINUS> PowerExpression()
        {
            jjtThis.jjtSetValue("UM");
        }
    ) #Operator(1)
      |
    (<NOT> PowerExpression()
        {
            jjtThis.jjtSetValue("!");
        }
    ) #Operator(1)
      |
    (<TILDE> PowerExpression()
        {
            jjtThis.jjtSetValue("~");
        }
    ) #Operator(1)
}

void PrimaryExpression() : {}
{
    Constant()
      |
    LOOKAHEAD(3)
    (<MINUS> Constant()
        {
            jjtThis.jjtSetValue("UM");
        }
    ) #Operator(1)
      |
    Identifier()
      |
    <LRND> OrExpression() <RRND> 
      |
    UnaryExpression()
      |
    MatrixExpression()
}

void MatrixExpression() #Matrix : {}
{
    <LSQ> CommaExpression() (<SEMI> CommaExpression())* <RSQ>
}

// TODO: Make () an operation that applies to any expression, not just variables and functions.
void Identifier() #Identifier : {Token t; Token p = null;}
{
    t = <IDENTIFIER> [p = <LRND> [CommaExpression()] <RRND>]
    {
        Identifier ID = new Identifier (t.image, t.beginColumn, t.endColumn);
        ID.hadParens =  p != null;
        jjtThis.jjtSetValue (ID);
    }
}

void Constant() #Constant : {Token t; Token u; String unitName = "";}
{
    t = <STRING>
    {
        Text text = new Text (t.image.substring (1, t.image.length() - 1));
        text.removeEscapes ();
        jjtThis.jjtSetValue (text);
    }
      |
    t = <NUMBER> (u = <UNIT> {unitName += u.image;})*
    {
        UnitValue result = new UnitValue ();
        try
        {
            String image = t.image.toLowerCase ();
            boolean hex = image.startsWith ("0x");
            if      (hex                    ) result.value = Integer.decode (t.image);
            else if (image.equals (".inf")  ) result.value = Double.POSITIVE_INFINITY;
            else if (image.equals ("\u221e")) result.value = Double.POSITIVE_INFINITY;
            else if (image.equals (".nan")  ) result.value = Double.NaN;
            else                              result.value = Double.parseDouble (t.image);
            if (hex) image = image.substring (2);  // Get rid of 0x
            else     image = image.split ("e")[0];  // Get rid of exponent
            image = image.replace ("-", "");
            image = image.replace (".", "");
            while (image.startsWith ("0")) image = image.substring (1);
            while (image.endsWith   ("0")) image = image.substring (0, image.length () - 1);
            result.digits = image.length ();
            if (hex) result.digits = (int) Math.ceil (result.digits * 1.2);  // ratio of (4 bits per hex digit) / (3.32 bits per decimal digit)
        }
        catch (NumberFormatException e)
        {
            ParseException p = new ParseException ("Malformed number");
            t.next = t;  // A hack to force error reporting to find the correct column.
            p.currentToken = t;
            throw p;
        }
        if (! unitName.isEmpty ())
        {
            try
            {
                if (namedUnits  != null) result.unit = namedUnits    .get   (unitName);
                if (result.unit == null) result.unit = UnitValue.UCUM.parse (unitName);
            }
            catch (MeasurementParseException e)
            {
                ParseException p = new ParseException ("UCUM syntax error");
                p.currentToken = t;
                t.next.beginColumn += e.getPosition ();
                throw p;
            }
            catch (TokenException e)
            {
                ParseException p = new ParseException ("UCUM syntax error");
                p.currentToken = t;
                throw p;
            }
        }
        jjtThis.jjtSetValue (result);
    }
}
